buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}

	dependencies {
		classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
	}
}

import org.apache.tools.ant.filters.*

apply plugin: 'com.github.hierynomus.license'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

// default tasks
defaultTasks 'clean', 'license', 'check', 'build', 'install'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'de.blablubbabc'
version = projectVersion

ext.projectName = projectName
ext.url = url
ext.description = description
ext.authors = authors
ext.author = authors.split(',\\s*')[0]

repositories {
	mavenLocal()
	maven {
		url 'https://hub.spigotmc.org/nexus/content/groups/public/'
	}
	maven {
		url 'http://repo.dmulloy2.net/nexus/repository/public/'
	}
	mavenCentral()
}

dependencies {
	compile 'org.bukkit:bukkit:1.13.1-R0.1-SNAPSHOT'
	compile 'com.comphenix.protocol:ProtocolLib:4.4.0'
}

// include and filter resources
processResources {
	from ('LICENSE.txt')
	from(sourceSets.main.resources.srcDirs)
	filter {
			line ->
			line
			.replace('${name}', project.name)
			.replace('${version}', project.version)
			.replace('${author}', project.author)
			.replace('${authors}', project.authors)
			.replace('${url}', project.url)
			.replace('${description}', project.description)
	}
}

// license header
license {
	ext.name = project.name
	ext.author = project.author
	ext.authors = project.authors
	ext.url = project.url

	header file('HEADER.txt')
	include '**/*.java'
	exclude '**/thirdparty/**'

	ignoreFailures true
	strictCheck true
}

// source compiler configuration
configure([compileJava, compileTestJava]) {
	//options.compilerArgs += [ '-Xlint:all', '-Xlint:-path' ]
	options.deprecation = false
	options.encoding = 'utf8'
}

artifacts {
	archives jar
}

uploadArchives {
	repositories {
		mavenDeployer {
			mavenLocal()
		}
	}
}
