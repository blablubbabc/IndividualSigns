plugins {
	id 'com.github.hierynomus.license' version '0.16.1'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

import org.apache.tools.ant.filters.*


apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'

eclipse {
	project {
		name = 'IndividualSigns'
	}
}

// default tasks
defaultTasks 'clean', 'license', 'check', 'build', 'publishToMavenLocal'

sourceCompatibility = '1.16'
targetCompatibility = '1.16'

group = 'de.blablubbabc.insigns'
version = projectVersion

ext.projectName = projectName
ext.url = url
ext.description = description
ext.authors = authors
ext.author = authors.split(',\\s*')[0]

repositories {
	mavenLocal()
	maven {
		url 'https://hub.spigotmc.org/nexus/content/groups/public/'
	}
	maven {
		url 'https://repo.dmulloy2.net/nexus/repository/public/'
	}
	// bstats
	maven {
		url 'https://repo.codemc.org/repository/maven-public'
	}
	mavenCentral()
}

configurations {
	shade
	compileClasspath.extendsFrom shade
}

dependencies {
	compileOnly 'org.bukkit:bukkit:1.17.1-R0.1-SNAPSHOT'
	compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
	shade 'org.bstats:bstats-bukkit:2.2.1'
}

// include and filter resources
processResources {
	duplicatesStrategy = 'exclude'

	from ('LICENSE.txt')
	from(sourceSets.main.resources.srcDirs)
	filter {
			line ->
			line
			.replace('${name}', project.name)
			.replace('${version}', project.version)
			.replace('${author}', project.author)
			.replace('${authors}', project.authors)
			.replace('${url}', project.url)
			.replace('${description}', project.description)
	}
}

// license header
license {
	ext.name = project.name
	ext.author = project.author
	ext.authors = project.authors
	ext.url = project.url

	header file('HEADER.txt')
	include '**/*.java'
	exclude '**/thirdparty/**'

	ignoreFailures true
	strictCheck true
}

// source compiler configuration
configure([compileJava, compileTestJava]) {
	//options.compilerArgs += [ '-Xlint:all', '-Xlint:-path' ]
	options.deprecation = false
	options.encoding = 'utf8'
}

shadowJar {
	configurations = [project.configurations.shade]
	archiveClassifier.set('')
	relocate 'org.bstats', project.group + '.metrics.bstats'
}

artifacts {
	archives shadowJar
}

publishing {
	publications {
		shadow(MavenPublication) {
			from components.java
		}
	}
}
